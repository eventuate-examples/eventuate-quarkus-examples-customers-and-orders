import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "io.quarkus" version "${quarkusVersion}"
    id 'org.kordamp.gradle.jandex' version '0.6.0'
}

apply plugin: 'docker-compose'

dockerCompose.isRequiredBy(test)

dockerCompose {
    environment.put "EVENTUATE_COMMON_VERSION", eventuateCommonImageVersion
    environment.put "EVENTUATE_MESSAGING_KAFKA_IMAGE_VERSION", eventuateMessagingKafkaImageVersion
    environment.put "EVENTUATE_CDC_VERSION", eventuateCdcImageVersion
    environment.put "EVENTUATE_JAVA_BASE_IMAGE_VERSION", eventuateExamplesBaseImageVersion

    projectName = null

    startedServices = ["customers-service"] 

    if ("mssql".equals(System.getenv("EVENTUATEDATABASE")))
        useComposeFiles = ['../docker-compose-eventuate-local-mssql-polling.yml']
    else if ("postgresql".equals(System.getenv("EVENTUATEDATABASE")))
        useComposeFiles = ['../docker-compose-eventuate-local-postgres-wal.yml']
    else
        useComposeFiles = ['../docker-compose-eventuate-local-mysql.yml']
}

dependencies {
    api project(":orders-common")

    api "io.eventuate.local.java:eventuate-client-java-quarkus"
    api "io.eventuate.local.java:eventuate-local-java-quarkus-jdbc"
    api "io.eventuate.common:eventuate-common-quarkus-jdbc:$eventuateCommonQuarkusVersion"

    api 'io.quarkus:quarkus-core'
    api 'io.quarkus.arc:arc'
    api 'io.quarkus:quarkus-arc-deployment'
    api 'io.quarkus:quarkus-smallrye-health'
    api 'io.quarkus:quarkus-smallrye-openapi'
    api 'io.quarkus:quarkus-resteasy-jackson'

    api 'io.quarkus:quarkus-jdbc-mysql'
    api 'io.quarkus:quarkus-jdbc-mssql'
    api 'io.quarkus:quarkus-jdbc-postgresql'

    api 'io.quarkus:quarkus-rest-client'
    api 'io.quarkus:quarkus-resteasy-jackson'

    api project(":customers-common")

    testImplementation "io.eventuate.common:eventuate-common-quarkus-jdbc-test-configuration:$eventuateCommonQuarkusVersion"

    testImplementation 'org.mockito:mockito-all:1.10.19'
    testImplementation "io.eventuate.local.java:eventuate-client-java-quarkus-jdbc"
    testImplementation 'io.quarkus:quarkus-junit5'
}

test {
    useJUnitPlatform()
}

processResources {
    outputs.upToDateWhen { false }
    filter ReplaceTokens, tokens: ["EVENTUATEDATABASE": System.env.EVENTUATEDATABASE ?: "mysql"]
}
